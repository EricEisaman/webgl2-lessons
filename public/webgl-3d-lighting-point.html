<p>Title: WebGL 3D - Point Lighting<br />Description: How to implement point lighting in WebGL<br />TOC: Point Lighting</p>

<p>This article is a continuation of <a href="webgl-3d-lighting-directional.html">WebGL 3D Directional Lighting</a>.<br />If you haven't read that I suggest <a href="webgl-3d-lighting-directional.html">you start there</a>.</p>

<p>In the last article we covered directional lighting where the light is coming<br />universally from the same direction. We set that direction before rendering.</p>

<p>What if instead of setting the direction for the light we picked a point in 3d space for the light<br />and computed the direction from any spot on the surface of our model in our shader?<br />That would give us a point light.</p>

<p>{{{diagram url="resources/point-lighting.html" width="500" height="400" className="noborder" }}}</p>

<p>If you rotate the surface above you'll see how each point on the surface has a different<br /><em>surface to light</em> vector. Getting the dot product of the surface normal and each individual<br />surface to light vector gives us a different value at each point on the surface.</p>

<p>So, let's do that.</p>

<p>First we need the light position</p>

<pre><code>uniform vec3 u_lightWorldPosition;
</code></pre>

<p>And we need a way to compute the world position of the surface. For that we can multiply<br />our positions by the world matrix so ...</p>

<pre><code>uniform mat4 u_world;

...

// compute the world position of the surface
vec3 surfaceWorldPosition = (u_world * a_position).xyz;
</code></pre>

<p>And we can compute a vector from the surface to the light which is similar to the<br />direction we had before expect this time we're computing it for every position on the<br />surface to a point.</p>

<pre><code>v_surfaceToLight = u_lightPosition - surfaceWorldPosition;
</code></pre>

<p>Here's all that in context</p>

<pre><code>#version 300 es

in vec4 a_position;
in vec3 a_normal;

+uniform vec3 u_lightWorldPosition;

+uniform mat4 u_world;
uniform mat4 u_worldViewProjection;
uniform mat4 u_worldInverseTranspose;

out vec3 v_normal;
+out vec3 v_surfaceToLight;

void main() {
  // Multiply the position by the matrix.
  gl_Position = u_worldViewProjection * a_position;

  // orient the normals and pass to the fragment shader
  v_normal = mat3(u_worldInverseTranspose) * a_normal;

+  // compute the world position of the surface
+  vec3 surfaceWorldPosition = (u_world * a_position).xyz;
+
+  // compute the vector of the surface to the light
+  // and pass it to the fragment shader
+  v_surfaceToLight = u_lightWorldPosition - surfaceWorldPosition;
}
</code></pre>

<p>Now in the fragment shader we need to normalize the surface to light vector<br />since it's a not a unit vector. Note that we could normalize in the vertex shader<br />but because it's a <em>varying</em> it will be linearly interpolated between our positions<br />and so would not be a complete unit vector</p>

<pre><code>#version 300 es
precision highp float;

// Passed in from the vertex shader.
in vec3 v_normal;
+in vec3 v_surfaceToLight;

-uniform vec3 u_reverseLightDirection;
uniform vec4 u_color;

// we need to declare an output for the fragment shader
out vec4 outColor;

void main() {
  // because v_normal is a varying it's interpolated
  // so it will not be a unit vector. Normalizing it
  // will make it a unit vector again
  vec3 normal = normalize(v_normal);

  vec3 surfaceToLightDirection = normalize(v_surfaceToLight);

-  float light = dot(v_normal, u_reverseLightDirection);
+  float light = dot(v_normal, surfaceToLightDirection);

  outColor = u_color;

  // Lets multiply just the color portion (not the alpha)
  // by the light
  outColor.rgb *= light;
}
</code></pre>

<p>Then we need to lookup the locations of <code>u_world</code> and <code>u_lightWorldPosition</code></p>

<div class="highlight"><pre lang="">-  var reverseLightDirectionLocation =
-      gl.getUniformLocation(program, "u_reverseLightDirection");
+  var lightWorldPositionLocation =
+      gl.getUniformLocation(program, "u_lightWorldPosition");
+  var worldLocation =
+      gl.getUniformLocation(program, "u_world");
</pre></div>

<p>and set them</p>

<p><div class="highlight"><pre lang="">  // Set the matrices<br />+  gl.uniformMatrix4fv(<br />+      worldLocation, false,<br />+      worldMatrix);<br />  gl.uniformMatrix4fv(<br />      worldViewProjectionLocation, false,<br />      worldViewProjectionMatrix);</p>

<p>...</p>

<ul>
<li>// set the light direction.</li>
<li>gl.uniform3fv(reverseLightDirectionLocation, normalize([0.5, 0.7, 1]));</li>
<li>// set the light position</li>
<li>gl.uniform3fv(lightWorldPositionLocation, [20, 30, 50]);
</pre></div></li>
</ul>

<p>And here it is</p>

<p>{{{example url="../webgl-3d-lighting-point.html" }}}</p>

<p>Now that we have a point we can add something called specular highlighting.</p>

<p>If you look at on object in the real world, if it's remotely shiny then if it happens<br />to reflect the light directly at you it's almost like a mirror</p>

<p><img class="webgl_center" src="resources/specular-highlights.jpg" /></p>

<p>We can simulate that effect by computing if the light reflects into our eyes. Again the <em>dot-product</em><br />comes to the rescue.</p>

<p>What do we need to check? Well let's think about it. Light reflects at the same angle it hits a surface<br />so if the direction of the surface to the light is the exact reflection of the surface to the eye<br />then it's at the perfect angle to reflect</p>

<p>{{{diagram url="resources/surface-reflection.html" width="500" height="400" className="noborder" }}}</p>

<p>If we know the direction from the surface of our model to the light (which we do since we just did that).<br />And if we know the direction from the surface to view/eye/camera, which we can compute, then we can add<br />those 2 vectors and normalize them to get the <code>halfVector</code> which is the vector that sits half way between them.<br />If the halfVector and the surface normal match then it's the perfect angle to reflect the light into<br />the view/eye/camera. And how can we tell when they match? Take the <em>dot product</em> just like we did<br />before. 1 = they match, same direction, 0 = they're perpendicular, -1 = they're opposite.</p>

<p>{{{diagram url="resources/specular-lighting.html" width="500" height="400" className="noborder" }}}</p>

<p>So first thing is we need to pass in the view/camera/eye position, compute the surface to view vector<br />and pass it to the fragment shader.</p>

<pre><code>#version 300 es

in vec4 a_position;
in vec3 a_normal;

uniform vec3 u_lightWorldPosition;
+uniform vec3 u_viewWorldPosition;

uniform mat4 u_world;
uniform mat4 u_worldViewProjection;
uniform mat4 u_worldInverseTranspose;

varying vec3 v_normal;

out vec3 v_surfaceToLight;
+out vec3 v_surfaceToView;

void main() {
  // Multiply the position by the matrix.
  gl_Position = u_worldViewProjection * a_position;

  // orient the normals and pass to the fragment shader
  v_normal = mat3(u_worldInverseTranspose) * a_normal;

  // compute the world position of the surface
  vec3 surfaceWorldPosition = (u_world * a_position).xyz;

  // compute the vector of the surface to the light
  // and pass it to the fragment shader
  v_surfaceToLight = u_lightWorldPosition - surfaceWorldPosition;

+  // compute the vector of the surface to the view/camera
+  // and pass it to the fragment shader
+  v_surfaceToView = u_viewWorldPosition - surfaceWorldPosition;
}
</code></pre>

<p>Next in the fragment shader we need to compute the <code>halfVector</code> between<br />the surface to view and surface to light vectors. Then we can take the dot<br />product the <code>halfVector</code> and the normal to find out if the light is reflecting<br />into the view.</p>

<pre><code>// Passed in from the vertex shader.
in vec3 v_normal;
in vec3 v_surfaceToLight;
+in vec3 v_surfaceToView;

uniform vec4 u_color;

out vec4 outColor;

void main() {
  // because v_normal is a varying it's interpolated
  // so it will not be a unit vector. Normalizing it
  // will make it a unit vector again
  vec3 normal = normalize(v_normal);

+  vec3 surfaceToLightDirection = normalize(v_surfaceToLight);
+  vec3 surfaceToViewDirection = normalize(v_surfaceToView);
+  vec3 halfVector = normalize(surfaceToLightDirection + surfaceToViewDirection);

  float light = dot(normal, surfaceToLightDirection);
+  float specular = dot(normal, halfVector);

  outColor = u_color;

  // Lets multiply just the color portion (not the alpha)
  // by the light
  outColor.rgb *= light;

+  // Just add in the specular
+  outColor.rgb += specular;
}
</code></pre>

<p>Finally we have to look up <code>u_viewWorldPosition</code> and set it</p>

<pre><code>var lightWorldPositionLocation =
    gl.getUniformLocation(program, "u_lightWorldPosition");
+var viewWorldPositionLocation =
+    gl.getUniformLocation(program, "u_viewWorldPosition");

...

// Compute the camera's matrix
var camera = [100, 150, 200];
var target = [0, 35, 0];
var up = [0, 1, 0];
var cameraMatrix = makeLookAt(camera, target, up);

...

+// set the camera/view position
+gl.uniform3fv(viewWorldPositionLocation, camera);
</code></pre>

<p>And here's that</p>

<p>{{{example url="../webgl-3d-lighting-point-specular.html" }}}</p>

<p><strong>DANG THAT'S BRIGHT!</strong></p>

<p>We can fix the brightness by raising the dot-product result to a power. This will scrunch up<br />the specular highlight from a linear falloff to an exponential falloff.</p>

<p>{{{diagram url="resources/power-graph.html" width="300" height="300" className="noborder" }}}</p>

<p>The closer the red line is to the top of the graph the brighter our specular addition<br />will be. By raising the power it scrunches the range where it goes bright to the<br />right.</p>

<p>Let's call that <code>shininess</code> and add it to our shader.</p>

<pre><code>uniform vec4 u_color;
+uniform float u_shininess;

...

-  float specular = dot(normal, halfVector);
+  float specular = 0.0;
+  if (light &gt; 0.0) {
+    specular = pow(dot(normal, halfVector), u_shininess);
+  }
</code></pre>

<p>The dot product can go negative. Taking a negative number to a power is undefined in WebGL<br />which would be bad. So, if the dot product would possibly be negative then we just leave specular at 0.0.</p>

<p>Of course we need to look up the location and set it</p>

<pre><code>+var shininessLocation = gl.getUniformLocation(program, "u_shininess");

...

// set the shininess
gl.uniform1f(shininessLocation, shininess);
</code></pre>

<p>And here's that</p>

<p>{{{example url="../webgl-3d-lighting-point-specular-power.html" }}}</p>

<p>The final thing I want to go over in this article is light colors.</p>

<p>Up to this point we've been using <code>light</code> to multiply the color we're passing in for the<br />F. We could provide a light color as well if wanted colored lights</p>

<pre><code>uniform vec4 u_color;
uniform float u_shininess;
+uniform vec3 u_lightColor;
+uniform vec3 u_specularColor;

...

  // Lets multiply just the color portion (not the alpha)
  // by the light
*  outColor.rgb *= light * u_lightColor;

  // Just add in the specular
*  outColor.rgb += specular * u_specularColor;
}
</code></pre>

<p>and of course</p>

<pre><code>+  var lightColorLocation =
+      gl.getUniformLocation(program, "u_lightColor");
+  var specularColorLocation =
+      gl.getUniformLocation(program, "u_specularColor");
</code></pre>

<p>and</p>

<pre><code>+  // set the light color
+  gl.uniform3fv(lightColorLocation, normalize([1, 0.6, 0.6]));  // red light
+  // set the specular color
+  gl.uniform3fv(specularColorLocation, normalize([1, 0.6, 0.6]));  // red light
</code></pre>

<p>{{{example url="../webgl-3d-lighting-point-color.html" }}}</p>

<p>Coming up next <a href="webgl-3d-lighting-spot.html">spot lighting</a>.</p>

<div class="webgl_bottombar">
<h3>Why is <code>pow(negative, power)</code> undefined?</h3>
<p>What does this mean?</p>
<div class="webgl_center"><pre class="glocal-center-content">pow(5, 2)</pre></div>
<p>Well you can look at it as</p>
<div class="webgl_center"><pre class="glocal-center-content">5 * 5 = 25</pre></div>
<p>What about</p>
<div class="webgl_center"><pre class="glocal-center-content">pow(5, 3)</pre></div>
<p>Well you can look at that as</p>
<div class="webgl_center"><pre class="glocal-center-content">5 * 5 * 5 = 125</pre></div>
<p>Ok, how about</p>
<div class="webgl_center"><pre class="glocal-center-content">pow(-5, 2)</pre></div>
<p>Well that could be</p>
<div class="webgl_center"><pre class="glocal-center-content">-5 * -5 = 25</pre></div>
<p>And</p>
<div class="webgl_center"><pre class="glocal-center-content">pow(-5, 3)</pre></div>
<p>Well you can look at as</p>
<div class="webgl_center"><pre class="glocal-center-content">-5 * -5 * -5 = -125</pre></div>
<p>As you know multiplying a negative by a negative makes a positive. Multiplying by a negative
again makes it negative.</p>
<p>Well then what does this mean?</p>
<div class="webgl_center"><pre class="glocal-center-content">pow(-5, 2.5)</pre></div>
<p>How do you decide which is the result of that positive or negative? That's
the land of <a href="https://betterexplained.com/articles/a-visual-intuitive-guide-to-imaginary-numbers/">imaginary numbers</a>.</p>
</div>