<p>Title: WebGL Shaders and GLSL<br />Description: What's a shader and what's GLSL<br />TOC: Shaders and GLSL</p>

<p>This is a continuation from <a href="webgl-fundamentals.html">WebGL Fundamentals</a>.<br />If you haven't read about how WebGL works you might want to <a href="webgl-how-it-works.html">read this first</a>.</p>

<p>We've talked about shaders and GLSL but haven't really given them any specific details.<br />I think I was hoping it would be clear by example but let's try to make it clearer just in case.</p>

<p>As mentioned in <a href="webgl-how-it-works.html">how it works</a> WebGL requires 2 shaders every time you<br />draw something. A <em>vertex shader</em> and a <em>fragment shader</em>. Each shader is a <em>function</em>. A vertex<br />shader and fragment shader are linked together into a shader program (or just program). A typical<br />WebGL app will have many shader programs.</p>

<h2>Vertex Shader</h2>

<p>A Vertex Shader's job is to generate clipspace coordinates. It always takes the form</p>

<pre><code>#version 300 es
void main() {
   gl_Position = doMathToMakeClipspaceCoordinates
}
</code></pre>

<p>Your shader is called once per vertex. Each time it's called you are required to set the special global variable, <code>gl_Position</code> to some clipspace coordinates.</p>

<p>Vertex shaders need data. They can get that data in 3 ways.</p>

<ol>
<li><a href="#attributes">Attributes</a> (data pulled from buffers)</li>
<li><a href="#uniforms">Uniforms</a> (values that stay the same for all vertices of a single draw call)</li>
<li><a href="#textures-in-vertex-shaders">Textures</a> (data from pixels/texels)</li>
</ol>

<h3>Attributes</h3>

<p>The most common way for a vertex shader to get data is through buffers and <em>attributes</em>.<br /><a href="webgl-how-it-works.html">How it works</a> covered buffers and<br />attributes. You create buffers,</p>

<pre><code>var buf = gl.createBuffer();
</code></pre>

<p>put data in those buffers</p>

<pre><code>gl.bindBuffer(gl.ARRAY_BUFFER, buf);
gl.bufferData(gl.ARRAY_BUFFER, someData, gl.STATIC_DRAW);
</code></pre>

<p>Then, given a shader program you made you look up the location of its attributes,</p>

<pre><code>var positionLoc = gl.getAttribLocation(someShaderProgram, "a_position");
</code></pre>

<p>then tell WebGL how to pull data out of those buffers and into the attribute</p>

<pre><code>// turn on getting data out of a buffer for this attribute
gl.enableVertexAttribArray(positionLoc);

var numComponents = 3;  // (x, y, z)
var type = gl.FLOAT;
var normalize = false;  // leave the values as they are
var offset = 0;         // start at the beginning of the buffer
var stride = 0;         // how many bytes to move to the next vertex
                        // 0 = use the correct stride for type and numComponents

gl.vertexAttribPointer(positionLoc, numComponents, type, false, stride, offset);
</code></pre>

<p>In <a href="webgl-fundamentals.html">WebGL fundamentals</a> we showed that we can do no math<br />in the shader and just pass the data directly through.</p>

<pre><code>#version 300 es

in vec4 a_position;

void main() {
   gl_Position = a_position;
}
</code></pre>

<p>If we put clipspace vertices into our buffers it will work.</p>

<p>Attributes can use <code>float</code>, <code>vec2</code>, <code>vec3</code>, <code>vec4</code>, <code>mat2</code>, <code>mat3</code>, <code>mat4</code>,<br /><code>int</code>, <code>ivec2</code>, <code>ivec3</code>, <code>ivec4</code>, <code>uint</code>, <code>uvec2</code>, <code>uvec3</code>, <code>uvec4</code> as types.</p>

<h3>Uniforms</h3>

<p>For a vertex shader uniforms are values passed to the vertex shader that stay the same<br />for all vertices in a draw call. As a very simple example we could add an offset to<br />the vertex shader above</p>

<pre><code>#version 300 es

in vec4 a_position;
+uniform vec4 u_offset;

void main() {
   gl_Position = a_position + u_offset;
}
</code></pre>

<p>And now we could offset every vertex by a certain amount. First we'd look up the<br />location of the uniform</p>

<pre><code>var offsetLoc = gl.getUniformLocation(someProgram, "u_offset");
</code></pre>

<p>And then before drawing we'd set the uniform</p>

<pre><code>gl.uniform4fv(offsetLoc, [1, 0, 0, 0]);  // offset it to the right half the screen
</code></pre>

<p>Uniforms can be many types. For each type you have to call the corresponding function to set it.</p>

<pre><code>gl.uniform1f (floatUniformLoc, v);                 // for float
gl.uniform1fv(floatUniformLoc, [v]);               // for float or float array
gl.uniform2f (vec2UniformLoc,  v0, v1);            // for vec2
gl.uniform2fv(vec2UniformLoc,  [v0, v1]);          // for vec2 or vec2 array
gl.uniform3f (vec3UniformLoc,  v0, v1, v2);        // for vec3
gl.uniform3fv(vec3UniformLoc,  [v0, v1, v2]);      // for vec3 or vec3 array
gl.uniform4f (vec4UniformLoc,  v0, v1, v2, v4);    // for vec4
gl.uniform4fv(vec4UniformLoc,  [v0, v1, v2, v4]);  // for vec4 or vec4 array

gl.uniformMatrix2fv(mat2UniformLoc, false, [  4x element array ])  // for mat2 or mat2 array
gl.uniformMatrix3fv(mat3UniformLoc, false, [  9x element array ])  // for mat3 or mat3 array
gl.uniformMatrix4fv(mat4UniformLoc, false, [ 16x element array ])  // for mat4 or mat4 array

gl.uniform1i (intUniformLoc,   v);                 // for int
gl.uniform1iv(intUniformLoc, [v]);                 // for int or int array
gl.uniform2i (ivec2UniformLoc, v0, v1);            // for ivec2
gl.uniform2iv(ivec2UniformLoc, [v0, v1]);          // for ivec2 or ivec2 array
gl.uniform3i (ivec3UniformLoc, v0, v1, v2);        // for ivec3
gl.uniform3iv(ivec3UniformLoc, [v0, v1, v2]);      // for ivec3 or ivec3 array
gl.uniform4i (ivec4UniformLoc, v0, v1, v2, v4);    // for ivec4
gl.uniform4iv(ivec4UniformLoc, [v0, v1, v2, v4]);  // for ivec4 or ivec4 array

gl.uniform1u (intUniformLoc,   v);                 // for uint
gl.uniform1uv(intUniformLoc, [v]);                 // for uint or uint array
gl.uniform2u (ivec2UniformLoc, v0, v1);            // for uvec2
gl.uniform2uv(ivec2UniformLoc, [v0, v1]);          // for uvec2 or uvec2 array
gl.uniform3u (ivec3UniformLoc, v0, v1, v2);        // for uvec3
gl.uniform3uv(ivec3UniformLoc, [v0, v1, v2]);      // for uvec3 or uvec3 array
gl.uniform4u (ivec4UniformLoc, v0, v1, v2, v4);    // for uvec4
gl.uniform4uv(ivec4UniformLoc, [v0, v1, v2, v4]);  // for uvec4 or uvec4 array

// for sampler2D, sampler3D, samplerCube, samplerCubeShader, sampler2DShadow,
// sampler2DArray, sampler2DArrayShadow
gl.uniform1i (samplerUniformLoc,   v);
gl.uniform1iv(samplerUniformLoc, [v]);
</code></pre>

<p>There's also types <code>bool</code>, <code>bvec2</code>, <code>bvec3</code>, and <code>bvec4</code>. They use either the <code>gl.uniform?f?</code>, <code>gl.uniform?i?</code>,<br />or <code>gl.uniform?u?</code> functions.</p>

<p>Note that for an array you can set all the uniforms of the array at once. For example</p>

<pre><code>// in shader
uniform vec2 u_someVec2[3];

// in JavaScript at init time
var someVec2Loc = gl.getUniformLocation(someProgram, "u_someVec2");

// at render time
gl.uniform2fv(someVec2Loc, [1, 2, 3, 4, 5, 6]);  // set the entire array of u_someVec2
</code></pre>

<p>But if you want to set individual elements of the array you must look up the location of<br />each element individually.</p>

<pre><code>// in JavaScript at init time
var someVec2Element0Loc = gl.getUniformLocation(someProgram, "u_someVec2[0]");
var someVec2Element1Loc = gl.getUniformLocation(someProgram, "u_someVec2[1]");
var someVec2Element2Loc = gl.getUniformLocation(someProgram, "u_someVec2[2]");

// at render time
gl.uniform2fv(someVec2Element0Loc, [1, 2]);  // set element 0
gl.uniform2fv(someVec2Element1Loc, [3, 4]);  // set element 1
gl.uniform2fv(someVec2Element2Loc, [5, 6]);  // set element 2
</code></pre>

<p>Similarly if you create a struct</p>

<pre><code>struct SomeStruct {
  bool active;
  vec2 someVec2;
};
uniform SomeStruct u_someThing;
</code></pre>

<p>you have to look up each field individually</p>

<pre><code>var someThingActiveLoc = gl.getUniformLocation(someProgram, "u_someThing.active");
var someThingSomeVec2Loc = gl.getUniformLocation(someProgram, "u_someThing.someVec2");
</code></pre>

<h3>Textures in Vertex Shaders</h3>

<p>See <a href="#textures-in-fragment-shaders">Textures in Fragment Shaders</a>.</p>

<h2>Fragment Shader</h2>

<p>A Fragment Shader's job is to provide a color for the current pixel being rasterized.<br />It always takes the form</p>

<pre><code>#version 300 es
precision highp float;

out vec4 outColor;  // you can pick any name

void main() {
   outColor = doMathToMakeAColor;
}
</code></pre>

<p>Your fragment shader is called once per pixel. Each time it's called you are required<br />to set your out variable to some color.</p>

<p>Fragment shaders need data. They can get data in 3 ways</p>

<ol>
<li><a href="#uniforms">Uniforms</a> (values that stay the same for every pixel of a single draw call)</li>
<li><a href="#textures-in-fragment-shaders">Textures</a> (data from pixels/texels)</li>
<li><a href="#varyings">Varyings</a> (data passed from the vertex shader and interpolated)</li>
</ol>

<h3>Uniforms in Fragment Shaders</h3>

<p>See <a href="#uniforms">Uniforms in Vertex Shaders</a>.</p>

<h3>Textures in Fragment Shaders</h3>

<p>Getting a value from a texture in a shader we create a <code>sampler2D</code> uniform and use the GLSL<br />function <code>texture</code> to extract a value from it.</p>

<pre><code>precision highp float;

uniform sampler2D u_texture;

out vec4 outColor;

void main() {
   vec2 texcoord = vec2(0.5, 0.5)  // get a value from the middle of the texture
   outColor = texture(u_texture, texcoord);
}
</code></pre>

<p>What data comes out of the texture is <a href="webgl-3d-textures.html">dependent on many settings</a>.<br />At a minimum we need to create and put data in the texture, for example</p>

<pre><code>var tex = gl.createTexture();
gl.bindTexture(gl.TEXTURE_2D, tex);
var level = 0;
var internalFormat = gl.RGBA,
var width = 2;
var height = 1;
var border = 0; // MUST ALWAYS BE ZERO
var format = gl.RGBA;
var type = gl.UNSIGNED_BYTE;
var data = new Uint8Array([255, 0, 0, 255, 0, 255, 0, 255]);
gl.texImage2D(gl.TEXTURE_2D,
              level,
              internalFormat,
              width,
              height,
              border,
              format,
              type,
              data);
</code></pre>

<p>Then look up the uniform location in the shader program</p>

<pre><code>var someSamplerLoc = gl.getUniformLocation(someProgram, "u_texture");
</code></pre>

<p>WebGL then requires you to bind it to a texture unit</p>

<pre><code>var unit = 5;  // Pick some texture unit
gl.activeTexture(gl.TEXTURE0 + unit);
gl.bindTexture(gl.TEXTURE_2D, tex);
</code></pre>

<p>And tell the shader which unit you bound the texture to</p>

<pre><code>gl.uniform1i(someSamplerLoc, unit);
</code></pre>

<h3>Varyings</h3>

<p>A varying is a way to pass a value from a vertex shader to a fragment shader which we<br />covered in <a href="webgl-how-it-works.html">how it works</a>.</p>

<p>To use a varying we need to declare matching varyings in both a vertex and fragment shader.<br />We set the <em>out</em> varying in the vertex shader with some value per vertex. When WebGL draws pixels<br />it will optionally interpolate between those values and pass them to the corresponding <em>in</em> varying in<br />the fragment shader</p>

<p>Vertex shader</p>

<pre><code>#version 300 es

in vec4 a_position;

uniform vec4 u_offset;

+out vec4 v_positionWithOffset;

void main() {
  gl_Position = a_position + u_offset;
+  v_positionWithOffset = a_position + u_offset;
}
</code></pre>

<p>Fragment shader</p>

<pre><code>#version 300 es
precision highp float;

+in vec4 v_positionWithOffset;

out vec4 outColor;

void main() {
+  // convert from clipsapce (-1 &lt;-&gt; +1) to color space (0 -&gt; 1).
+  vec4 color = v_positionWithOffset * 0.5 + 0.5;
+  outColor = color;
}
</code></pre>

<p>The example above is a mostly nonsense example. It doesn't generally make sense to<br />directly copy the clip space values to the fragment shader and use them as colors. Nevertheless<br />it will work and produce colors.</p>

<h2>GLSL</h2>

<p>GLSL stands for <a href="https://www.khronos.org/registry/gles/specs/3.0/GLSL_ES_Specification_3.00.3.pdf">Graphics Library Shader Language</a>.<br />It's the language shaders are written<br />in. It has some special semi unique features that are certainly not common in JavaScript.<br />It's designed to do the math that is commonly needed to compute things for rasterizing<br />graphics. So for example it has built in types like <code>vec2</code>, <code>vec3</code>, and <code>vec4</code> which<br />represent 2 values, 3 values, and 4 values respectively. Similarly it has <code>mat2</code>, <code>mat3</code><br />and <code>mat4</code> which represent 2x2, 3x3, and 4x4 matrices. You can do things like multiply<br />a <code>vec</code> by a scalar.</p>

<pre><code>vec4 a = vec4(1, 2, 3, 4);
vec4 b = a * 2.0;
// b is now vec4(2, 4, 6, 8);
</code></pre>

<p>Similarly it can do matrix multiplication and vector to matrix multiplication</p>

<pre><code>mat4 a = ???
mat4 b = ???
mat4 c = a * b;

vec4 v = ???
vec4 y = c * v;
</code></pre>

<p>It also has various selectors for the parts of a vec. For a vec4</p>

<pre><code>vec4 v;
</code></pre>

<ul>
<li><code>v.x</code> is the same as <code>v.s</code> and <code>v.r</code> and <code>v[0]</code>.</li>
<li><code>v.y</code> is the same as <code>v.t</code> and <code>v.g</code> and <code>v[1]</code>.</li>
<li><code>v.z</code> is the same as <code>v.p</code> and <code>v.b</code> and <code>v[2]</code>.</li>
<li><code>v.w</code> is the same as <code>v.q</code> and <code>v.a</code> and <code>v[3]</code>.</li>
</ul>

<p>It it able to <em>swizzle</em> vec components which means you can swap or repeat components.</p>

<pre><code>v.yyyy
</code></pre>

<p>is the same as</p>

<pre><code>vec4(v.y, v.y, v.y, v.y)
</code></pre>

<p>Similarly</p>

<pre><code>v.bgra
</code></pre>

<p>is the same as</p>

<pre><code>vec4(v.b, v.g, v.r, v.a)
</code></pre>

<p>When constructing a vec or a mat you can supply multiple parts at once. So for example</p>

<pre><code>vec4(v.rgb, 1)
</code></pre>

<p>Is the same as</p>

<pre><code>vec4(v.r, v.g, v.b, 1)
</code></pre>

<p>One thing you'll likely get caught up on is that GLSL is very type strict.</p>

<pre><code>float f = 1;  // ERROR 1 is an int. You can't assign an int to a float
</code></pre>

<p>The correct way is one of these</p>

<pre><code>float f = 1.0;      // use float
float f = float(1)  // cast the integer to a float
</code></pre>

<p>The example above of <code>vec4(v.rgb, 1)</code> doesn't complain about the <code>1</code> because <code>vec4</code> is<br />casting the things inside just like <code>float(1)</code>.</p>

<p>GLSL has a bunch of built in functions. Many of them operate on multiple components at once.<br />So for example</p>

<pre><code>T sin(T angle)
</code></pre>

<p>Means T can be <code>float</code>, <code>vec2</code>, <code>vec3</code> or <code>vec4</code>. If you pass in <code>vec4</code> you get <code>vec4</code> back<br />which the sine of each of the components. In other words if <code>v</code> is a <code>vec4</code> then</p>

<pre><code>vec4 s = sin(v);
</code></pre>

<p>is the same as</p>

<pre><code>vec4 s = vec4(sin(v.x), sin(v.y), sin(v.z), sin(v.w));
</code></pre>

<p>Sometimes one argument is a float and the rest is <code>T</code>. That means that float will be applied<br />to all the components. For example if <code>v1</code> and <code>v2</code> are <code>vec4</code> and <code>f</code> is a float then</p>

<pre><code>vec4 m = mix(v1, v2, f);
</code></pre>

<p>is the same as</p>

<pre><code>vec4 m = vec4(
  mix(v1.x, v2.x, f),
  mix(v1.y, v2.y, f),
  mix(v1.z, v2.z, f),
  mix(v1.w, v2.w, f));
</code></pre>

<p>You can see a list of all the GLSL functions on the last 3 pages of <a href="https://www.khronos.org/files/opengles3-quick-reference-card.pdf">the OpenGL ES 3.0<br />Reference Card</a><br />If you like really dry and verbose stuff you can try<br /><a href="https://www.khronos.org/registry/gles/specs/3.0/GLSL_ES_Specification_3.00.3.pdf">the GLSL ES 3.00 spec</a>.</p>

<h2>Putting it all together</h2>

<p>That's the point of this entire series of posts. WebGL is all about creating various shaders, supplying<br />the data to those shaders and then calling <code>gl.drawArrays</code>, <code>gl.drawElements</code>, etc to have WebGL process<br />the vertices by calling the current vertex shader for each vertex and then render pixels by calling the<br />the current fragment shader for each pixel.</p>

<p>Actually creating the shaders requires several lines of code. Since those lines are the same in<br />most WebGL programs and since once written you can pretty much ignore them <a href="webgl-boilerplate.html">how to compile GLSL shaders<br />and link them into a shader program is covered here</a>.</p>

<p>If you're just starting from here you can go in 2 directions. If you are interested in image processing<br />I'll show you <a href="webgl-image-processing.html">how to do some 2D image processing</a>.<br />If you are interesting in learning about translation,<br />rotation and scale then <a href="webgl-2d-translation.html">start here</a>.</p>